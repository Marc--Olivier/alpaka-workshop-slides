from snakemake.utils import Paramspace
import pandas as pd
import os

current_dir=os.getcwd()
configfile: "config.yaml"

# define a paramspace
# filename_params defines naming and directory structure of every instance of the parameter space
paramspace = Paramspace(pd.read_csv(config["input_params"], sep=","), filename_params="*", param_sep="-")

# declare which rule should not be executed on the cluster
localrules: all, simulate, build_command

# target rule, defines entire anticipated output
rule all:
    input: expand("simulated/finished_{params}.txt", params=paramspace.instance_patterns)

# creates a string to set CMAKE flags in pic-build command
rule build_command:
    output: temp(f"commands/{paramspace.wildcard_pattern}.txt")
    params:
        sim_params=paramspace.instance
    run:
        dict_params = params.sim_params
        string = ";".join(f"-D{key}={value}" for key, value in dict_params.items())
        with open(str(output), "w") as text_file:
            text_file.write(string)

# compile for every parameter instance
rule compile:
    input:
        pic_project=config["pic_project"],
        pic_profile=config["pic_profile"],
        pic_build_command=rules.build_command.output
    # to use the paramspace naming pattern use python f-string formatting
    output: directory(f"simulations/sim_{paramspace.wildcard_pattern}")
    group: "compile"
    params:
        pic_cfg=config["pic_cfg"],
        # access one parameter set with the instance methode
        sim_params=paramspace.instance,
        # define name to use paramspace naming pattern in shell commands
        name=f"{paramspace.wildcard_pattern}"
    shell:
        """
        source {input.pic_profile}
        pic-create {input.pic_project} projects/run_{params.name}/
        cd projects/run_{params.name}/
        # append touch command to .tpl -> creates file when simulation is finished
        echo "if [[ ! -z \`grep \\\"100 % =\\\" output\` ]]; then touch {current_dir}/simulated/finished_{params.name}.txt; fi" >> $TBG_TPLFILE
        # parameter dependent compile
        build_command=$(<{current_dir}/{input.pic_build_command})
        pic-build -c \"-DPARAM_OVERWRITES:LIST=\'$build_command\'\"
        # create simulation directory
        tbg -t -c etc/picongpu/{params.pic_cfg} {current_dir}/{output}
        """

# start and track simulation
rule simulate:
    input: rules.compile.output
    output: f"simulated/finished_{paramspace.wildcard_pattern}.txt"
    params:
        name=f"{paramspace.wildcard_pattern}"
    retries: 0 # defines number of retries if execution fails
    shell:
        """
        # if no job id is known -> start simulation
        if ! [ -f simulated/job_id_{params.name}.txt ]; then
            sbatch {input}/tbg/submit.start > simulated/job_id_{params.name}.txt
        fi

        job_id=$(cut -c 21-27 simulated/job_id_{params.name}.txt)
        status=$(squeue --jobs $job_id -o "%2t" | tail -n 1 )

        # if job isn't running, pending or configuring -> restart job
        if ! [[ "$status" == "PD" || "$status" == "R " || "$status" == "CF" ]]; then
            sbatch {input}/tbg/submit.start >  simulated/job_id_{params.name}.txt
            job_id=$(cut -c 21-27 simulated/job_id_{params.name}.txt)
            status=$(squeue --jobs $job_id -o "%2t" | tail -n 1 )
        fi
        # wait till job is finished
        while [[ "$status" == "PD" || "$status" == "R " || "$status" == "CF" ]]
        do
            sleep 120
            status=$(squeue --jobs $job_id -o "%2t" | tail -n 1 )
        done
        """
